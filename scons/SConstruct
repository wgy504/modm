#!/usr/bin/env python3
# -*- coding: utf-8 -*-
#
# Copyright (c) 2016, Raphael Lehmann
# Copyright (c) 2016-2017, Niklas Hauser
# Copyright (c) 2016-2017, Sascha Schade
#
# This file is part of the modm project.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
# -----------------------------------------------------------------------------

"""
WARNING: THIS IS A GENERIC SCONSTRUCT FILE FOR ALL MODM PROJECTS!
         YOU NEED TO EXECUTE THIS FILE INSIDE YOUR PROJECTS SCONSTRUCT FILE:

# path to the modm root directory (modify as required!)
modmpath = '../modm'
# execute the modm basic
execfile(modmpath + '/scons/SConstruct')
"""

# This variable MUST be defined externally
# modmpath = '../../..'

# Create the SConstruct environment
env = Environment(tools = ['modm', 'bitmap'], toolpath = [modmpath + '/scons/site_tools'])

# build the modm library
env.ModmLibrary()
# create a file called 'defines.hpp' with all preprocessor defines if necessary
env.Defines()
# build modm_git_info.hpp file
# env.GitInfoHeader()   # disabled for privacy
# build modm_build_info.hpp file
# env.BuildInfoHeader() # disabled for privacy
# you may append these methods manually to your local SConstruct file!

# find all source files
env.Append(CPPPATH = ['.'])
files = env.FindFiles('.')

# add manually specified sources as well
if 'extrasources' in env['MODM_CONFIG']['build']:
    files.sources = files.sources + eval(env['MODM_CONFIG']['build']['extrasources'])

# add sources for MODM communication
if 'communication' in env['MODM_CONFIG']:
    if 'source' in env['MODM_CONFIG']['communication']:
        source_path = env['MODM_CONFIG']['communication']['source']
    else: source_path = 'communication.xml'
    if 'generate' in env['MODM_CONFIG']['communication']:
        generate_path = env['MODM_CONFIG']['communication']['generate']
    else: generate_path = 'communication'
    if 'namespace' in env['MODM_CONFIG']['communication']:
        namespace = env['MODM_CONFIG']['communication']['namespace']
    else: namespace = 'robot'

    files.sources += env.XpccCommunication(
        source_path,
        dtdPath=modmpath + '/tools/system_design/xml/dtd',
        path=generate_path,
        namespace=namespace)

if 'images' in env['MODM_CONFIG']:
    # create c-files from the bitmaps
    for image in Glob(env['MODM_CONFIG']['images']['source'] + '/*.pbm'):
        files += env.Bitmap(image)

# build the program
program = env.Program(target = env['MODM_PROJECT_NAME'], source = files.sources)

env.Alias('size', env.Size(program))
env.Alias('symbols', env.Symbols(program))
env.Alias('defines', env.ShowDefines())
env.Alias('qtcreator', env.QtCreatorProject(files))

if env.CheckArchitecture('hosted'):
    env.Alias('build', program)
    env.Alias('run', env.Run(program))
    env.Alias('all', ['build', 'run'])
else:
    hexfile = env.Hex(program)

    if 'avrdude' in env['MODM_CONFIG']:
        env.Alias('program', env.Avrdude(hexfile))
        env.Alias('fuse', env.AvrdudeFuses())
    elif 'black_magic_probe' in env['MODM_CONFIG']:
        env.Alias('program', env.BlackMagicProbe(program))
    elif 'openocd' in env['MODM_CONFIG']:
        env.Alias('program', env.OpenOcd(program))
        env.Alias('openocd-debug', env.OpenOcdDebug(program))
    elif 'lpclink' in env['MODM_CONFIG']:
        env.Alias('init', env.LpcLinkInit(program))
        env.Alias('program', env.LpcLinkFlash(program))
        env.Alias('lpclink-debug', env.LpcLinkDebug(program))
    # ST Link only reasonable on Windows, please use OpenOCD instead
    if env.CheckArchitecture('avr'):
        env.Alias('eeprom', env.Eeprom(program))
    else:
        env.Alias('debug', env.GdbDebug(program))
        env.Alias('bin', env.Bin(program))

        # Most STM32's can be programmed by USB DFU if board allows
        binfile = env.Bin(program)
        env.Alias('dfu', env.DfuStm32Programmer(binfile))

    env.Alias('listing', env.Listing(program))
    env.Alias('build', hexfile)
    env.Alias('all', ['build', 'size'])

env.Default('all')
